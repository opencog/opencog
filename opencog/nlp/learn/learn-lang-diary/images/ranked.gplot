# set term png small color
# set term png small
# set out 'ranked.png'

# set data style linespoints
# set data style points

#################################################
# Used to generate the word ranking graphs.
#
set term postscript eps enhanced color 28
set out 'ranked-word-obs.eps'
set yrange [1:100000]
set logscale x
set logscale y
set format x "10^{%L}"
set format y "10^{%L}"
set title "Observed counts of words"
set xlabel "Rank"
set ylabel "Number of observations"
plot "ranked-word-obs.dat" using 1:2 with lines linewidth 5 title "", \
     "ranked-word-obs.dat" using 1:(90000/$1 ** 1.1) with lines linewidth 5 title "rank^{-1.1}"


#################################################
# Used to generate the disjunct ranking graphs.
#
set term postscript eps enhanced color 28
set out 'ranked-dj-obs.eps'
set xrange [1:100000]
set yrange [1:14000]
set logscale x
set logscale y
set format y "10^{%L}"
set title "Observed counts of disjuncts"
set xlabel "Rank"
set ylabel "Number of observations"
plot "ranked-dj-obs.dat" using 1:2 with lines linewidth 5 title "", \
     "ranked-dj-obs.dat" using 1:(14000/$1 ** 0.85) with lines linewidth 5 title "rank^{-0.85}"

#################################################
# Used to generate the disjunct ranking graphs.
#
set term postscript eps enhanced color 28
set out 'ranked-cset-obs.eps'
set xrange [1:200000]
set yrange [1:1000]
set logscale x
set logscale y
set format y "10^{%L}"
set title "Observed count of connector sets"
set xlabel "Rank"
set ylabel "Number of observations"
plot "ranked-cset-obs.dat" using 1:2 with lines linewidth 5 title "", \
     "ranked-cset-obs.dat" using 1:(1000/$1 ** 0.6) with lines linewidth 5 title "rank^{-0.6}"

#################################################
# Used to generate the connector-set ranking graphs.
#
set term postscript eps enhanced color 28
set out 'ranked-support.eps'
set xrange [1:20000]
set yrange [1:20000]
set logscale x
set logscale y
set format y "10^{%L}"
set title "Ranking by support of the disjunct set"
set xlabel "Rank"
set ylabel "Size of the set"
plot "ranked-support.dat" using 1:2 with lines linewidth 5 title "", \
     "ranked-support.dat" using 1:(20000/$1 ** 1) with lines linewidth 5 title "rank^{-1}"

#################################################
# Used to generate the connector-set ranking graphs.
#
set term postscript eps enhanced color 28
set out 'ranked-avg.eps'
set yrange [1:6]
set ytics (1,2,3,4,5,6)
set logscale x
set logscale y
set title "Rank by average observations"
set xlabel "Rank"
set ylabel "Average observations per disjunct"
plot "ranked-avg.dat" using 1:2 with lines linewidth 5 title "", \
     "ranked-avg.dat" using 1:(5/$1 ** 0.25) with lines linewidth 5 title "rank^{-0.25}"

unset ytics
set ytics

#################################################
# Used to generate the connector-set ranking graphs.
#
set term postscript eps enhanced color 28
set out 'ranked-lengths.eps'
set yrange [:2000]
set logscale x
set logscale y
set format x "10^{%L}"
set format y "10^{%L}"
set title "Ranked RMS size of set of disjuncts"
set xlabel "Rank"
set ylabel "RMS size of disjunct set"
plot "ranked-lengths.dat" using 1:2 with lines linewidth 5 title "", \
     "ranked-lengths.dat" using 1:(1000/$1 ** 0.65) with lines linewidth 5 title "rank^{-0.65}"

#################################################
# Used to generate the word-mi ranking graphs.
#
set term postscript eps color
set out 'ranked-word-mi-hi-p.eps'
set logscale x
set title "MI of word"
set xlabel "Rank"
set ylabel "MI of word"
plot "ranked-word-mi-hi-p.dat" using 1:2 with lines linewidth 2 title ""


#################################################
# Used to generate the word-entropy ranking graphs.
#
set term postscript eps color
set out 'ranked-word-ent.eps'
set logscale x
set title "Ranking by fractional word entropy"
set xlabel "Rank"
set ylabel "Fractional Word Entropy"
plot "ranked-word-ent.dat" using 1:2 with lines linewidth 2 title ""


#################################################
# Used to generate the vertex-degree ranking graphs.
#
set term postscript eps color
set out 'ranked-avg-connectors.eps'
set logscale x
set logscale y
set title "Ranking by avgerage number of connectors"
set xlabel "Rank"
set ylabel "Average number of connetors"
plot "ranked-avg-connectors.dat" using 1:2 with lines linewidth 2 title "", \
     "ranked-avg-connectors.dat" using 1:(7/$1 ** 0.2) with lines linewidth 2 title "rank^-0.2"

#################################################
# Used to generate the similarity ranking graph.
#
set term postscript eps color
set out 'ranked-sims.eps'
set yrange [0.5:1]
set logscale x
set title "Ranking by cosine similarity"
set xlabel "Rank"
set ylabel "Cosimine similarity"
plot "ranked-sims.dat" using 1:2 with lines linewidth 2 title "", \
     "ranked-sims.dat" using 1:(1-(log($1)**3)/1800) with lines linewidth 2 title "1-log(rank)^3/1800"

